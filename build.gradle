plugins {
    id 'java'
    id 'org.springframework.boot' version '3.3.5'
    id 'io.spring.dependency-management' version '1.1.6'
    id "org.sonarqube" version "3.5.0.2730"
    id 'jacoco'
    id 'checkstyle'
    id 'com.github.spotbugs' version '5.0.13'
}

group = 'ru.tpu.hostel'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

sonarqube {
    properties {
        property "sonar.projectKey", "hostel-service" // Замените на ключ вашего проекта в SonarCloud
        property "sonar.organization", "hostel-service" // Замените на имя вашей организации
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.login", System.getenv("SONAR_TOKEN") // Используется токен из секретов GitHub
        property "sonar.coverage.jacoco.xmlReportPaths", "${layout.buildDirectory.get()}/reports/jacoco/test/jacocoTestReport.xml"
        property "sonar.java.spotbugs.reportPaths", "${layout.buildDirectory.get()}/reports/spotbugs/main.xml"
        property "sonar.coverage.exclusions", "**/*Generated*.java,**/model/*.java" // Исключения из анализа
        property "sonar.coverage.minimum", "80" // Минимальное покрытие (в процентах)
    }
}

checkstyle {
    toolVersion = "10.7" // Версия Checkstyle
    configFile = file("${rootDir}/config/checkstyle/checkstyle.xml") // Путь к конфигурации Checkstyle
}

spotbugs {
    toolVersion = "4.7.3"
    reportsDir = layout.buildDirectory.dir("reports/spotbugs").get().asFile
}

dependencies {
    compileOnly 'javax.servlet:javax.servlet-api:4.0.1'
    implementation 'org.springframework.boot:spring-boot-configuration-processor'
    implementation 'com.fasterxml.jackson.core:jackson-databind'
    implementation platform("org.springframework.cloud:spring-cloud-dependencies:2023.0.2")
    implementation 'commons-io:commons-io:2.14.0'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.3.0'
    runtimeOnly 'org.postgresql:postgresql'
    implementation "org.flywaydb:flyway-database-postgresql"
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'

    implementation 'jakarta.validation:jakarta.validation-api:3.0.2'

    implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
    implementation 'io.jsonwebtoken:jjwt-impl:0.11.5'
    implementation 'io.jsonwebtoken:jjwt-jackson:0.11.5'

    spotbugs 'com.github.spotbugs:spotbugs:4.7.3'

    compileOnly 'org.projectlombok:lombok'

    annotationProcessor 'org.projectlombok:lombok'
    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.boot:spring-boot-testcontainers'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'org.testcontainers:junit-jupiter'
    testImplementation 'org.testcontainers:postgresql'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.2'
    testImplementation 'org.mockito:mockito-core:5.2.0'
    testImplementation 'org.assertj:assertj-core:3.24.2'
    testImplementation 'org.instancio:instancio-core:2.13.0'
}

tasks.named('test') {
    useJUnitPlatform()
}

tasks.withType(Test).configureEach {
    finalizedBy jacocoTestReport // Генерация отчета после выполнения тестов
}

jacoco {
    toolVersion = "0.8.8" // Версия JaCoCo
}

jacocoTestReport {
    reports {
        xml.required.set(true) // XML-отчет для SonarCloud
        html.required.set(true) // HTML-отчет для локального просмотра
    }
}

jar {
    manifest {
        attributes 'Main-Class': 'ru.tpu.hostel.user.UserApplication'
    }
}
